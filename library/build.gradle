plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.dokka'
    id 'kotlin-kapt'
    id 'maven-publish'
}

group = 'org.dicekeys'
version = '1.0'

android {
    namespace = 'org.dicekeys.crypto.seeded'
    ndkVersion '27.3.13750724' // Latest LTS

    compileSdkVersion 36
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
                arguments "-DANDROID_STL=c++_shared", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            initWith debug
            jniDebuggable false
        }
    }

    externalNativeBuild {
        cmake {
            version "3.18.1+"
            path file('src/main/cpp/CMakeLists.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain 17
    }

    buildToolsVersion buildToolsVersion
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"
}


afterEvaluate {
    publishing {
        publications {
            // Only publish one artifact called "seeded"
            release(MavenPublication) {
                from components.release
                groupId = "org.dicekeys"
                artifactId = "seeded"
                version = "1.0"
            }
        }

        // âœ… Prevent duplicate artifacts (removes library.aar)
        publications.configureEach {
            if (name != "release") {
                it.artifacts.clear()
            }
        }
    }
}

